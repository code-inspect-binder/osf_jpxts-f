TITLE: Analysis 5
DATA: FILE = analysis4+5_S1.dat;
VARIABLE:
NAMES = ID WB content stressed lonely
FTF CMC mixed peers family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = WB
CMC mixed family weak_ties
weekend
cxf cxw mxf mxw;
CLUSTER = ID;
WITHIN = CMC mixed family weak_ties weekend
cxf cxw mxf mxw;
MISSING = .;
DEFINE:
CENTER CMC (GROUPMEAN);
CENTER mixed (GROUPMEAN);
CENTER family (GROUPMEAN);
CENTER weak_ties (GROUPMEAN);
cxf=CMC*family;
cxw=CMC*weak_ties;
mxf=mixed*family;
mxw=mixed*weak_ties;
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Well-being is predicted by CMC, the slope is random
S1 | WB ON CMC;
! Well-being is predicted by mixed episodes, the slope is random
S2 | WB ON mixed;
! Well-being is predicted by family, the slope is random
S3 | WB ON family;
! Well-being is predicted by weak ties, the slope is random
S4 | WB ON weak_ties;
! Well-being is predicted by CMC*family, the slope is random
S5 | WB ON cxf;
! Well-being is predicted by CMC*weak ties, the slope is random
S6 | WB ON cxw;
! Well-being is predicted by mixed*family, the slope is random
S7 | WB ON mxf;
! Well-being is predicted by mixed*weak ties, the slope is random
S8 | WB ON mxw;
! Control for weekend
WB ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
WB S1-S8 WITH WB S1-S8;
OUTPUT: STDYX STDY TECH8;