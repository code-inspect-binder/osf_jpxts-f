TITLE: Analysis 5
DATA: FILE = analysis4+5_S3.dat;
VARIABLE:
NAMES = ID AB angry worried happy sad WB stressed lonely
FTF CMC mixed peers family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = angry
CMC mixed family weak_ties
weekend A
cxf cxw mxf mxw;
CLUSTER = ID;
WITHIN = CMC mixed family weak_ties weekend
cxf cxw mxf mxw;
BETWEEN = A;
MISSING = .;
DEFINE:
CENTER CMC (GROUPMEAN);
CENTER mixed (GROUPMEAN);
CENTER family (GROUPMEAN);
CENTER weak_ties (GROUPMEAN);
cxf=CMC*family;
cxw=CMC*weak_ties;
mxf=mixed*family;
mxw=mixed*weak_ties;
CENTER A (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Anger is predicted by CMC, the slope is random
S1 | angry ON CMC;
! Anger is predicted by mixed episodes, the slope is random
S2 | angry ON mixed;
! Anger is predicted by family, the slope is random
S3 | angry ON family;
! Anger is predicted by weak ties, the slope is random
S4 | angry ON weak_ties;
! Anger is predicted by CMC*family, the slope is random
S5 | angry ON cxf;
! Anger is predicted by CMC*weak ties, the slope is random
S6 | angry ON cxw;
! Anger is predicted by mixed*family, the slope is random
S7 | angry ON mxf;
! Anger is predicted by mixed*weak ties, the slope is random
S8 | angry ON mxw;
! Control for weekend
angry ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
angry S1-S8 WITH angry S1-S8;
! The intercept is predicted by agreeableness
angry ON A;
! The slopes are predicted by agreeableness
S1-S8 ON A;
OUTPUT: STDYX STDY TECH8;