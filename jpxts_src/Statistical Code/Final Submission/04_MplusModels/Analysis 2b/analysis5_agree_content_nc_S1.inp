TITLE: Analysis 5
DATA: FILE = analysis4+5_S1.dat;
VARIABLE:
NAMES = ID WB content stressed lonely
FTF CMC mixed peers family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = content
CMC mixed family weak_ties
weekend A
cxf cxw mxf mxw;
CLUSTER = ID;
WITHIN = CMC mixed family weak_ties weekend
cxf cxw mxf mxw;
BETWEEN = A;
MISSING = .;
DEFINE:
CENTER CMC (GROUPMEAN);
CENTER mixed (GROUPMEAN);
CENTER family (GROUPMEAN);
CENTER weak_ties (GROUPMEAN);
cxf=CMC*family;
cxw=CMC*weak_ties;
mxf=mixed*family;
mxw=mixed*weak_ties;
CENTER A (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Contentment is predicted by CMC, the slope is random
S1 | content ON CMC;
! Contentment is predicted by mixed episodes, the slope is random
S2 | content ON mixed;
! Contentment is predicted by family, the slope is random
S3 | content ON family;
! Contentment is predicted by weak ties, the slope is random
S4 | content ON weak_ties;
! Contentment is predicted by CMC*family, the slope is random
S5 | content ON cxf;
! Contentment is predicted by CMC*weak ties, the slope is random
S6 | content ON cxw;
! Contentment is predicted by mixed*family, the slope is random
S7 | content ON mxf;
! Contentment is predicted by mixed*weak ties, the slope is random
S8 | content ON mxw;
! Control for weekend
content ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
content S1-S8 WITH content S1-S8;
! The intercept is predicted by agreeableness
content ON A;
! The slopes are predicted by agreeableness
S1-S8 ON A;
OUTPUT: STDYX STDY TECH8;