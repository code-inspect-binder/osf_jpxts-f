TITLE: Analysis 5
DATA: FILE = analysis4+5_S3.dat;
VARIABLE:
NAMES = ID AB angry worried happy sad WB stressed lonely
FTF CMC mixed peers family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = sad
CMC mixed family weak_ties
weekend A
cxf cxw mxf mxw;
CLUSTER = ID;
WITHIN = CMC mixed family weak_ties weekend
cxf cxw mxf mxw;
BETWEEN = A;
MISSING = .;
DEFINE:
CENTER CMC (GROUPMEAN);
CENTER mixed (GROUPMEAN);
CENTER family (GROUPMEAN);
CENTER weak_ties (GROUPMEAN);
cxf=CMC*family;
cxw=CMC*weak_ties;
mxf=mixed*family;
mxw=mixed*weak_ties;
CENTER A (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Sadness is predicted by CMC, the slope is random
S1 | sad ON CMC;
! Sadness is predicted by mixed episodes, the slope is random
S2 | sad ON mixed;
! Sadness is predicted by family, the slope is random
S3 | sad ON family;
! Sadness is predicted by weak ties, the slope is random
S4 | sad ON weak_ties;
! Sadness is predicted by CMC*family, the slope is random
S5 | sad ON cxf;
! Sadness is predicted by CMC*weak ties, the slope is random
S6 | sad ON cxw;
! Sadness is predicted by mixed*family, the slope is random
S7 | sad ON mxf;
! Sadness is predicted by mixed*weak ties, the slope is random
S8 | sad ON mxw;
! Control for weekend
sad ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
sad S1-S8 WITH sad S1-S8;
! The intercept is predicted by agreeableness
sad ON A;
! The slopes are predicted by agreeableness
S1-S8 ON A;
OUTPUT: STDYX STDY TECH8;