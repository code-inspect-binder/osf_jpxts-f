TITLE: Analysis 5
DATA: FILE = analysis4+5_S3.dat;
VARIABLE:
NAMES = ID AB angry worried happy sad WB stressed lonely
FTF CMC mixed peers family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = worried
CMC mixed family weak_ties
weekend C
cxf cxw mxf mxw;
CLUSTER = ID;
WITHIN = CMC mixed family weak_ties weekend
cxf cxw mxf mxw;
BETWEEN = C;
MISSING = .;
DEFINE:
CENTER CMC (GROUPMEAN);
CENTER mixed (GROUPMEAN);
CENTER family (GROUPMEAN);
CENTER weak_ties (GROUPMEAN);
cxf=CMC*family;
cxw=CMC*weak_ties;
mxf=mixed*family;
mxw=mixed*weak_ties;
CENTER C (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Worry is predicted by CMC, the slope is random
S1 | worried ON CMC;
! Worry is predicted by mixed episodes, the slope is random
S2 | worried ON mixed;
! Worry is predicted by family, the slope is random
S3 | worried ON family;
! Worry is predicted by weak ties, the slope is random
S4 | worried ON weak_ties;
! Worry is predicted by CMC*family, the slope is random
S5 | worried ON cxf;
! Worry is predicted by CMC*weak ties, the slope is random
S6 | worried ON cxw;
! Worry is predicted by mixed*family, the slope is random
S7 | worried ON mxf;
! Worry is predicted by mixed*weak ties, the slope is random
S8 | worried ON mxw;
! Control for weekend
worried ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
worried S1-S8 WITH worried S1-S8;
! The intercept is predicted by conscientiousness
worried ON C;
! The slopes are predicted by conscientiousness
S1-S8 ON C;
OUTPUT: STDYX STDY TECH8;