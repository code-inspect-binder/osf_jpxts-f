TITLE: Analysis 5
DATA: FILE = analysis4+5_S2.dat;
VARIABLE:
NAMES = ID WB content stressed lonely
FTF CMC mixed peers family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = WB
CMC mixed family weak_ties
weekend N gender ethnicity SES
cxf cxw mxf mxw;
CLUSTER = ID;
WITHIN = CMC mixed family weak_ties weekend
cxf cxw mxf mxw;
BETWEEN = N gender ethnicity SES;
MISSING = .;
DEFINE:
CENTER CMC (GROUPMEAN);
CENTER mixed (GROUPMEAN);
CENTER family (GROUPMEAN);
CENTER weak_ties (GROUPMEAN);
cxf=CMC*family;
cxw=CMC*weak_ties;
mxf=mixed*family;
mxw=mixed*weak_ties;
CENTER N (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Well-being is predicted by CMC, the slope is random
S1 | WB ON CMC;
! Well-being is predicted by mixed episodes, the slope is random
S2 | WB ON mixed;
! Well-being is predicted by family, the slope is random
S3 | WB ON family;
! Well-being is predicted by weak ties, the slope is random
S4 | WB ON weak_ties;
! Well-being is predicted by CMC*family, the slope is random
S5 | WB ON cxf;
! Well-being is predicted by CMC*weak ties, the slope is random
S6 | WB ON cxw;
! Well-being is predicted by mixed*family, the slope is random
S7 | WB ON mxf;
! Well-being is predicted by mixed*weak ties, the slope is random
S8 | WB ON mxw;
! Control for weekend
WB ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
WB S1-S8 WITH WB S1-S8;
! The intercept is predicted by neuroticism
WB ON N;
! The slopes are predicted by neuroticism
S1-S8 ON N;
! Control for demographic variables
WB ON gender ethnicity SES;
OUTPUT: STDYX STDY TECH8;