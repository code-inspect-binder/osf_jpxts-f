TITLE: Analysis 1
DATA: FILE = analysis1_S3.dat;
VARIABLE:
NAMES = ID AB angry worried happy sad WB stressed lonely
int
E A C N O gender ethnicity SES weekend;
USEVARIABLES = happy
int
weekend E;
CLUSTER = ID;
WITHIN = weekend;
BETWEEN = E;
MISSING = .;
DEFINE: CENTER E (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Happiness is predicted by social interactions, the slope is random
S1 | happy ON int;
! Control for weekend
happy ON weekend;
%BETWEEN%
! Covariance between intercept and slope
happy WITH S1;
! The intercept is predicted by social interactions
happy ON int;
! The intercept is predicted by extraversion
happy ON E;
! The slope is predicted by extraversion
S1 ON E;
OUTPUT: STDYX STDY TECH8;