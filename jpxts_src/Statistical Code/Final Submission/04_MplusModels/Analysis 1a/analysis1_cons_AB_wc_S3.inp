TITLE: Analysis 1
DATA: FILE = analysis1_S3.dat;
VARIABLE:
NAMES = ID AB angry worried happy sad WB stressed lonely
int
E A C N O gender ethnicity SES weekend;
USEVARIABLES = AB
int
weekend C gender ethnicity SES;
CLUSTER = ID;
WITHIN = weekend;
BETWEEN = C gender ethnicity SES;
MISSING = .;
DEFINE: CENTER C (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Affect balance is predicted by social interactions, the slope is random
S1 | AB ON int;
! Control for weekend
AB ON weekend;
%BETWEEN%
! Covariance between intercept and slope
AB WITH S1;
! The intercept is predicted by social interactions
AB ON int;
! The intercept is predicted by conscientiousness
AB ON C;
! The slope is predicted by conscientiousness
S1 ON C;
! Control for demographic variables
AB ON gender ethnicity SES;
OUTPUT: STDYX STDY TECH8;