TITLE: Analysis 1
DATA: FILE = analysis1_S1.dat;
VARIABLE:
NAMES = ID WB content stressed lonely
int
E A C N O gender ethnicity SES weekend;
USEVARIABLES = lonely
int
weekend N;
CLUSTER = ID;
WITHIN = weekend;
BETWEEN = N;
MISSING = .;
DEFINE: CENTER N (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Loneliness is predicted by social interactions, the slope is random
S1 | lonely ON int;
! Control for weekend
lonely ON weekend;
%BETWEEN%
! Covariance between intercept and slope
lonely WITH S1;
! The intercept is predicted by social interactions
lonely ON int;
! The intercept is predicted by neuroticism
lonely ON N;
! The slope is predicted by neuroticism
S1 ON N;
OUTPUT: STDYX STDY TECH8;