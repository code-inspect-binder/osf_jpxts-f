TITLE: Analysis 4
DATA: FILE = analysis4+5_suppl1_S3.dat;
VARIABLE:
NAMES = ID AB angry worried happy sad WB stressed lonely
TVC TCE SN peers family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = AB
TCE SN family weak_ties
weekend N;
CLUSTER = ID;
WITHIN = TCE SN family weak_ties weekend;
BETWEEN = N;
MISSING = .;
DEFINE:
CENTER TCE (GROUPMEAN);
CENTER SN (GROUPMEAN);
CENTER family (GROUPMEAN);
CENTER weak_ties (GROUPMEAN);
CENTER N (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Affect balance is predicted by TCE, the slope is random
S1 | AB ON TCE;
! Affect balance is predicted by SN, the slope is random
S2 | AB ON SN;
! Affect balance is predicted by family, the slope is random
S3 | AB ON family;
! Affect balance is predicted by weak ties, the slope is random
S4 | AB ON weak_ties;
! Control for weekend
AB ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
AB S1-S4 WITH AB S1-S4;
! The intercept is predicted by neuroticism
AB ON N;
! The slopes are predicted by neuroticism
S1-S4 ON N;
OUTPUT: STDYX STDY TECH8;