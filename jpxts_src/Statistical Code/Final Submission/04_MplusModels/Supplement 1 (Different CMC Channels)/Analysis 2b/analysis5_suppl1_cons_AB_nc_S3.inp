TITLE: Analysis 5
DATA: FILE = analysis4+5_suppl1_S3.dat;
VARIABLE:
NAMES = ID AB angry worried happy sad WB stressed lonely
TVC TCE SN peers family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = AB
TCE SN family weak_ties
weekend C
txf txw sxf sxw;
CLUSTER = ID;
WITHIN = TCE SN family weak_ties weekend
txf txw sxf sxw;
BETWEEN = C;
MISSING = .;
DEFINE:
CENTER TCE (GROUPMEAN);
CENTER SN (GROUPMEAN);
CENTER family (GROUPMEAN);
CENTER weak_ties (GROUPMEAN);
txf=TCE*family;
txw=TCE*weak_ties;
sxf=SN*family;
sxw=SN*weak_ties;
CENTER C (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Affect balance is predicted by TCE, the slope is random
S1 | AB ON TCE;
! Affect balance is predicted by SN, the slope is random
S2 | AB ON SN;
! Affect balance is predicted by family, the slope is random
S3 | AB ON family;
! Affect balance is predicted by weak ties, the slope is random
S4 | AB ON weak_ties;
! Affect balance is predicted by TCE*family, the slope is random
S5 | AB ON txf;
! Affect balance is predicted by TCE*weak ties, the slope is random
S6 | AB ON txw;
! Affect balance is predicted by SN*family, the slope is random
S7 | AB ON sxf;
! Affect balance is predicted by SN*weak ties, the slope is random
S8 | AB ON sxw;
! Control for weekend
AB ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
AB S1-S8 WITH AB S1-S8;
! The intercept is predicted by conscientiousness
AB ON C;
! The slopes are predicted by conscientiousness
S1-S8 ON C;
OUTPUT: STDYX STDY TECH8;