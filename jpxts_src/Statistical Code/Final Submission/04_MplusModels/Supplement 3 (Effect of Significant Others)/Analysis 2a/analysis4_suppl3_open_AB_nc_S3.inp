TITLE: Analysis 4
DATA: FILE = analysis4+5_suppl3_S3.dat;
VARIABLE:
NAMES = ID AB angry worried happy sad WB stressed lonely
FTF CMC mixed FR SO family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = AB
CMC mixed SO family weak_ties
weekend O;
CLUSTER = ID;
WITHIN = CMC mixed SO family weak_ties weekend;
BETWEEN = O;
MISSING = .;
DEFINE:
CENTER CMC (GROUPMEAN);
CENTER mixed (GROUPMEAN);
CENTER SO (GROUPMEAN);
CENTER family (GROUPMEAN);
CENTER weak_ties (GROUPMEAN);
CENTER O (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Affect balance is predicted by CMC, the slope is random
S1 | AB ON CMC;
! Affect balance is predicted by mixed episodes, the slope is random
S2 | AB ON mixed;
! Affect balance is predicted by significant others, the slope is random
S3 | AB ON SO;
! Affect balance is predicted by family, the slope is random
S4 | AB ON family;
! Affect balance is predicted by weak ties, the slope is random
S5 | AB ON weak_ties;
! Control for weekend
AB ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
AB S1-S5 WITH AB S1-S5;
! The intercept is predicted by openness
AB ON O;
! The slopes are predicted by openness
S1-S5 ON O;
OUTPUT: STDYX STDY TECH8;