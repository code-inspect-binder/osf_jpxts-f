TITLE: Analysis 4
DATA: FILE = analysis4+5_S3.dat;
VARIABLE:
NAMES = ID AB angry worried happy sad WB stressed lonely
FTF CMC mixed peers family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = angry
CMC mixed family weak_ties
weekend O;
CLUSTER = ID;
WITHIN = CMC mixed family weak_ties weekend;
BETWEEN = O;
MISSING = .;
DEFINE:
CENTER CMC (GROUPMEAN);
CENTER mixed (GROUPMEAN);
CENTER family (GROUPMEAN);
CENTER weak_ties (GROUPMEAN);
CENTER O (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Anger is predicted by CMC, the slope is random
S1 | angry ON CMC;
! Anger is predicted by mixed episodes, the slope is random
S2 | angry ON mixed;
! Anger is predicted by family, the slope is random
S3 | angry ON family;
! Anger is predicted by weak ties, the slope is random
S4 | angry ON weak_ties;
! Control for weekend
angry ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
angry S1-S4 WITH angry S1-S4;
! The intercept is predicted by openness
angry ON O;
! The slopes are predicted by openness
S1-S4 ON O;
OUTPUT: STDYX STDY TECH8;