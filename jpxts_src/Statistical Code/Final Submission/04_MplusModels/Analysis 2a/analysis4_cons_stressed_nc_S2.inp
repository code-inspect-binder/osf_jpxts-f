TITLE: Analysis 4
DATA: FILE = analysis4+5_S2.dat;
VARIABLE:
NAMES = ID WB content stressed lonely
FTF CMC mixed peers family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = stressed
CMC mixed family weak_ties
weekend C;
CLUSTER = ID;
WITHIN = CMC mixed family weak_ties weekend;
BETWEEN = C;
MISSING = .;
DEFINE:
CENTER CMC (GROUPMEAN);
CENTER mixed (GROUPMEAN);
CENTER family (GROUPMEAN);
CENTER weak_ties (GROUPMEAN);
CENTER C (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Stress is predicted by CMC, the slope is random
S1 | stressed ON CMC;
! Stress is predicted by mixed episodes, the slope is random
S2 | stressed ON mixed;
! Stress is predicted by family, the slope is random
S3 | stressed ON family;
! Stress is predicted by weak ties, the slope is random
S4 | stressed ON weak_ties;
! Control for weekend
stressed ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
stressed S1-S4 WITH stressed S1-S4;
! The intercept is predicted by conscientiousness
stressed ON C;
! The slopes are predicted by conscientiousness
S1-S4 ON C;
OUTPUT: STDYX STDY TECH8;