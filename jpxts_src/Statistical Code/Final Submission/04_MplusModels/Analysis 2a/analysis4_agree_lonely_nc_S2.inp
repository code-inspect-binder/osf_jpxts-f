TITLE: Analysis 4
DATA: FILE = analysis4+5_S2.dat;
VARIABLE:
NAMES = ID WB content stressed lonely
FTF CMC mixed peers family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = lonely
CMC mixed family weak_ties
weekend A;
CLUSTER = ID;
WITHIN = CMC mixed family weak_ties weekend;
BETWEEN = A;
MISSING = .;
DEFINE:
CENTER CMC (GROUPMEAN);
CENTER mixed (GROUPMEAN);
CENTER family (GROUPMEAN);
CENTER weak_ties (GROUPMEAN);
CENTER A (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Loneliness is predicted by CMC, the slope is random
S1 | lonely ON CMC;
! Loneliness is predicted by mixed episodes, the slope is random
S2 | lonely ON mixed;
! Loneliness is predicted by family, the slope is random
S3 | lonely ON family;
! Loneliness is predicted by weak ties, the slope is random
S4 | lonely ON weak_ties;
! Control for weekend
lonely ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
lonely S1-S4 WITH lonely S1-S4;
! The intercept is predicted by agreeableness
lonely ON A;
! The slopes are predicted by agreeableness
S1-S4 ON A;
OUTPUT: STDYX STDY TECH8;