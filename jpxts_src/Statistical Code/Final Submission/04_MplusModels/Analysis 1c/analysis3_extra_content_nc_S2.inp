TITLE: Analysis 2+3
DATA: FILE = analysis3_S2.dat;
VARIABLE:
NAMES = ID WB content stressed lonely
peers family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = content
peers family weak_ties
weekend E;
CLUSTER = ID;
WITHIN = weekend;
BETWEEN = E;
MISSING = .;
DEFINE: CENTER E (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Contentment is predicted by peers, the slope is random
S1 | content ON peers;
! Contentment is predicted by family, the slope is random
S2 | content ON family;
! Contentment is predicted by weak ties, the slope is random
S3 | content ON weak_ties;
! Control for weekend
content ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
content S1-S3 WITH content S1-S3;
! The intercept is predicted by peers
content ON peers;
! The intercept is predicted by family
content ON family;
! The intercept is predicted by weak ties
content ON weak_ties;
! The intercept is predicted by extraversion
content ON E;
! The slopes are predicted by extraversion
S1-S3 ON E;
OUTPUT: STDYX STDY TECH8;