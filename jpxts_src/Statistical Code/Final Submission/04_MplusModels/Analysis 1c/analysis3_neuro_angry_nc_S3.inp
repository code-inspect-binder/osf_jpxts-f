TITLE: Analysis 2+3
DATA: FILE = analysis3_S3.dat;
VARIABLE:
NAMES = ID AB angry worried happy sad WB stressed lonely
peers family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = angry
peers family weak_ties
weekend N;
CLUSTER = ID;
WITHIN = weekend;
BETWEEN = N;
MISSING = .;
DEFINE: CENTER N (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Anger is predicted by peers, the slope is random
S1 | angry ON peers;
! Anger is predicted by family, the slope is random
S2 | angry ON family;
! Anger is predicted by weak ties, the slope is random
S3 | angry ON weak_ties;
! Control for weekend
angry ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
angry S1-S3 WITH angry S1-S3;
! The intercept is predicted by peers
angry ON peers;
! The intercept is predicted by family
angry ON family;
! The intercept is predicted by weak ties
angry ON weak_ties;
! The intercept is predicted by neuroticism
angry ON N;
! The slopes are predicted by neuroticism
S1-S3 ON N;
OUTPUT: STDYX STDY TECH8;