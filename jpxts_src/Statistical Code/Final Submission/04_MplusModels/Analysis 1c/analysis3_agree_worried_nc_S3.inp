TITLE: Analysis 2+3
DATA: FILE = analysis3_S3.dat;
VARIABLE:
NAMES = ID AB angry worried happy sad WB stressed lonely
peers family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = worried
peers family weak_ties
weekend A;
CLUSTER = ID;
WITHIN = weekend;
BETWEEN = A;
MISSING = .;
DEFINE: CENTER A (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Worry is predicted by peers, the slope is random
S1 | worried ON peers;
! Worry is predicted by family, the slope is random
S2 | worried ON family;
! Worry is predicted by weak ties, the slope is random
S3 | worried ON weak_ties;
! Control for weekend
worried ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
worried S1-S3 WITH worried S1-S3;
! The intercept is predicted by peers
worried ON peers;
! The intercept is predicted by family
worried ON family;
! The intercept is predicted by weak ties
worried ON weak_ties;
! The intercept is predicted by agreeableness
worried ON A;
! The slopes are predicted by agreeableness
S1-S3 ON A;
OUTPUT: STDYX STDY TECH8;