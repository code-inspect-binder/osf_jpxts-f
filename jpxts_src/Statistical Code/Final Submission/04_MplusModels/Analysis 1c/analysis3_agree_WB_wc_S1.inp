TITLE: Analysis 2+3
DATA: FILE = analysis3_S1.dat;
VARIABLE:
NAMES = ID WB content stressed lonely
peers family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = WB
peers family weak_ties
weekend A gender ethnicity SES;
CLUSTER = ID;
WITHIN = weekend;
BETWEEN = A gender ethnicity SES;
MISSING = .;
DEFINE: CENTER A (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Well-being is predicted by peers, the slope is random
S1 | WB ON peers;
! Well-being is predicted by family, the slope is random
S2 | WB ON family;
! Well-being is predicted by weak ties, the slope is random
S3 | WB ON weak_ties;
! Control for weekend
WB ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
WB S1-S3 WITH WB S1-S3;
! The intercept is predicted by peers
WB ON peers;
! The intercept is predicted by family
WB ON family;
! The intercept is predicted by weak ties
WB ON weak_ties;
! The intercept is predicted by agreeableness
WB ON A;
! The slopes are predicted by agreeableness
S1-S3 ON A;
! Control for demographic variables
WB ON gender ethnicity SES;
OUTPUT: STDYX STDY TECH8;