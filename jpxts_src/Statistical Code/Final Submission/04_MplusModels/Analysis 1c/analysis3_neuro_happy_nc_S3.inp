TITLE: Analysis 2+3
DATA: FILE = analysis3_S3.dat;
VARIABLE:
NAMES = ID AB angry worried happy sad WB stressed lonely
peers family weak_ties
E A C N O gender ethnicity SES weekend;
USEVARIABLES = happy
peers family weak_ties
weekend N;
CLUSTER = ID;
WITHIN = weekend;
BETWEEN = N;
MISSING = .;
DEFINE: CENTER N (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Happiness is predicted by peers, the slope is random
S1 | happy ON peers;
! Happiness is predicted by family, the slope is random
S2 | happy ON family;
! Happiness is predicted by weak ties, the slope is random
S3 | happy ON weak_ties;
! Control for weekend
happy ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
happy S1-S3 WITH happy S1-S3;
! The intercept is predicted by peers
happy ON peers;
! The intercept is predicted by family
happy ON family;
! The intercept is predicted by weak ties
happy ON weak_ties;
! The intercept is predicted by neuroticism
happy ON N;
! The slopes are predicted by neuroticism
S1-S3 ON N;
OUTPUT: STDYX STDY TECH8;