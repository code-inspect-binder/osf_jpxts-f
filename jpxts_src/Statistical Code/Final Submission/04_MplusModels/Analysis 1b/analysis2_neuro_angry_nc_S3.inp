TITLE: Analysis 2+3
DATA: FILE = analysis2_S3.dat;
VARIABLE:
NAMES = ID AB angry worried happy sad WB stressed lonely
FTF CMC mixed
E A C N O gender ethnicity SES weekend;
USEVARIABLES = angry
FTF CMC mixed
weekend N;
CLUSTER = ID;
WITHIN = weekend;
BETWEEN = N;
MISSING = .;
DEFINE: CENTER N (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Anger is predicted by FtF interactions, the slope is random
S1 | angry ON FTF;
! Anger is predicted by CMC, the slope is random
S2 | angry ON CMC;
! Anger is predicted by mixed episodes, the slope is random
S3 | angry ON mixed;
! Control for weekend
angry ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
angry S1-S3 WITH angry S1-S3;
! The intercept is predicted by FtF interactions
angry ON FTF;
! The intercept is predicted by CMC
angry ON CMC;
! The intercept is predicted by mixed episodes
angry ON mixed;
! The intercept is predicted by neuroticism
angry ON N;
! The slopes are predicted by neuroticism
S1-S3 ON N;
OUTPUT: STDYX STDY TECH8;