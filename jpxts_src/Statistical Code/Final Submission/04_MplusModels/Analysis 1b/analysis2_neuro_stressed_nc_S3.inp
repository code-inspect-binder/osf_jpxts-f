TITLE: Analysis 2+3
DATA: FILE = analysis2_S3.dat;
VARIABLE:
NAMES = ID AB angry worried happy sad WB stressed lonely
FTF CMC mixed
E A C N O gender ethnicity SES weekend;
USEVARIABLES = stressed
FTF CMC mixed
weekend N;
CLUSTER = ID;
WITHIN = weekend;
BETWEEN = N;
MISSING = .;
DEFINE: CENTER N (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Stress is predicted by FtF interactions, the slope is random
S1 | stressed ON FTF;
! Stress is predicted by CMC, the slope is random
S2 | stressed ON CMC;
! Stress is predicted by mixed episodes, the slope is random
S3 | stressed ON mixed;
! Control for weekend
stressed ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
stressed S1-S3 WITH stressed S1-S3;
! The intercept is predicted by FtF interactions
stressed ON FTF;
! The intercept is predicted by CMC
stressed ON CMC;
! The intercept is predicted by mixed episodes
stressed ON mixed;
! The intercept is predicted by neuroticism
stressed ON N;
! The slopes are predicted by neuroticism
S1-S3 ON N;
OUTPUT: STDYX STDY TECH8;