TITLE: Analysis 2+3
DATA: FILE = analysis2_S3.dat;
VARIABLE:
NAMES = ID AB angry worried happy sad WB stressed lonely
FTF CMC mixed
E A C N O gender ethnicity SES weekend;
USEVARIABLES = AB
FTF CMC mixed
weekend C;
CLUSTER = ID;
WITHIN = weekend;
BETWEEN = C;
MISSING = .;
DEFINE: CENTER C (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Affect balance is predicted by FtF interactions, the slope is random
S1 | AB ON FTF;
! Affect balance is predicted by CMC, the slope is random
S2 | AB ON CMC;
! Affect balance is predicted by mixed episodes, the slope is random
S3 | AB ON mixed;
! Control for weekend
AB ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
AB S1-S3 WITH AB S1-S3;
! The intercept is predicted by FtF interactions
AB ON FTF;
! The intercept is predicted by CMC
AB ON CMC;
! The intercept is predicted by mixed episodes
AB ON mixed;
! The intercept is predicted by conscientiousness
AB ON C;
! The slopes are predicted by conscientiousness
S1-S3 ON C;
OUTPUT: STDYX STDY TECH8;