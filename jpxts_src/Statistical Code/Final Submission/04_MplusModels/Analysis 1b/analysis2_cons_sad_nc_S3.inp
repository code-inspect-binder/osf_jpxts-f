TITLE: Analysis 2+3
DATA: FILE = analysis2_S3.dat;
VARIABLE:
NAMES = ID AB angry worried happy sad WB stressed lonely
FTF CMC mixed
E A C N O gender ethnicity SES weekend;
USEVARIABLES = sad
FTF CMC mixed
weekend C;
CLUSTER = ID;
WITHIN = weekend;
BETWEEN = C;
MISSING = .;
DEFINE: CENTER C (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Sadness is predicted by FtF interactions, the slope is random
S1 | sad ON FTF;
! Sadness is predicted by CMC, the slope is random
S2 | sad ON CMC;
! Sadness is predicted by mixed episodes, the slope is random
S3 | sad ON mixed;
! Control for weekend
sad ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
sad S1-S3 WITH sad S1-S3;
! The intercept is predicted by FtF interactions
sad ON FTF;
! The intercept is predicted by CMC
sad ON CMC;
! The intercept is predicted by mixed episodes
sad ON mixed;
! The intercept is predicted by conscientiousness
sad ON C;
! The slopes are predicted by conscientiousness
S1-S3 ON C;
OUTPUT: STDYX STDY TECH8;