TITLE: Analysis 2+3
DATA: FILE = analysis2_S1.dat;
VARIABLE:
NAMES = ID WB content stressed lonely
FTF CMC mixed
E A C N O gender ethnicity SES weekend;
USEVARIABLES = content
FTF CMC mixed
weekend E;
CLUSTER = ID;
WITHIN = weekend;
BETWEEN = E;
MISSING = .;
DEFINE: CENTER E (GRANDMEAN);
ANALYSIS: TYPE = TWOLEVEL RANDOM; ESTIMATOR = BAYES;
BITERATIONS = (10000); PROCESSORS = 2;
MODEL:
%WITHIN%
! Contentment is predicted by FtF interactions, the slope is random
S1 | content ON FTF;
! Contentment is predicted by CMC, the slope is random
S2 | content ON CMC;
! Contentment is predicted by mixed episodes, the slope is random
S3 | content ON mixed;
! Control for weekend
content ON weekend;
%BETWEEN%
! Covariance between intercept and slopes
content S1-S3 WITH content S1-S3;
! The intercept is predicted by FtF interactions
content ON FTF;
! The intercept is predicted by CMC
content ON CMC;
! The intercept is predicted by mixed episodes
content ON mixed;
! The intercept is predicted by extraversion
content ON E;
! The slopes are predicted by extraversion
S1-S3 ON E;
OUTPUT: STDYX STDY TECH8;